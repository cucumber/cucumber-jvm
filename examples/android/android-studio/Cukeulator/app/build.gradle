import com.android.ddmlib.AndroidDebugBridge
import com.android.ddmlib.IDevice
import com.android.ddmlib.CollectingOutputReceiver

apply plugin: 'com.android.application'

// ==================================================================
// Android configuration
// ==================================================================

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "cucumber.cukeulator"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testApplicationId "cucumber.cukeulator.test"
        testInstrumentationRunner "cucumber.cukeulator.test.CucumberRunner"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }

    // With the following option we does not have to mock everything,
    // e.g. super calls cannot be mocked with Mockito only (just with Powermock).
    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// ==================================================================
// Project dependencies
// ==================================================================

dependencies {
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1'

    // Use the stable Cucumber version
    androidTestCompile 'io.cucumber:cucumber-android:3.0.1'
    //
    // The following dependency works, if you build Cucumber-JVM on your local machine:
    //
    // androidTestCompile 'io.cucumber:cucumber-android:2.3.2-SNAPSHOT'
    //
    // (If you enable it, you must disable the dependency to the stable Cucumber version above.)
    //
    // If you have not yet built Cucumber-JVM on your local machine, build it with:
    //
    //                   cd <cucumber-jvm-source-root>
    //                   mvn clean install
    //
    // Hint: you find the Cucumber-JVM source root under a parent directory of this file.
    //
    // If you only change the Android source, you also can do:
    //
    //                   cd <cucumber-jvm-source-root>/android
    //                   mvn install
    //
    // Now, after you have been built Cucumber-JVM with one of the commands above, it is published
    // to your local Maven repository and may be used by other projects on your local machine.
    //
    // For our example project, that means you can execute the feature files, with the just built
    // Cucumber-JVM library by using the following command (press Alt+F12 in Android Studio):
    //
    //                   gradlew connectedCheck --refresh-dependencies
    //
    // The --refresh-dependencies option seems not to be required anymore for recent Android Studio
    // and Gradle versions. But, if your Cucumber-JVM snapshot dependency is not updated
    // automatically by Gradle, the flag always bypasses any caching of dependencies.

    androidTestCompile 'io.cucumber:cucumber-picocontainer:3.0.1'

    // compile 'com.android.support:appcompat-v7:25.1.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.10.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test:rules:1.0.1'
}

// ==================================================================
// Custom tasks
// ==================================================================

/*
 * Downloads all Cucumber reports from the connected device.
 */
task downloadCucumberReports {
    group "Verification"
    description "Downloads the rich Cucumber report files (HTML, XML, JSON) from the connected devices"

    doLast {
        getADB().devices.each { device ->
            def deviceSourcePath = getDeviceCucumberPath(device)
            def localReportPath = new File(buildDir, "reports/cucumber")
            def deviceSpecificReportPath = new File(buildDir, "reports/cucumber-${device.getSerialNumber()}")
            if (!localReportPath.exists()) {
                localReportPath.mkdirs()
            }
            if (!localReportPath.exists()) {
                throw new GradleException("Could not create $localReportPath")
            }
            if (!deviceSpecificReportPath.exists()) {
                deviceSpecificReportPath.mkdirs()
            }
            if (!deviceSpecificReportPath.exists()) {
                throw new GradleException("Could not create $deviceSpecificReportPath")
            }
            def adb = adbPath()
            def files = getCucumberReportFileNames()
            files.each { fileName ->
                //Write to main cucumber directory
                exec { commandLine adb, "-s", device.getSerialNumber(), "pull", deviceSourcePath+'/'+fileName, localReportPath }

                //Write to device specific cucumber directory
                exec { commandLine adb, "-s", device.getSerialNumber(), "pull", deviceSourcePath+'/'+fileName, deviceSpecificReportPath }
            }
        }
    }
}

/**
 * Deletes existing Cucumber reports on the device.
 */
task deleteExistingCucumberReports {
    group "Verification"
    description "Removes the rich Cucumber report files (HTML, XML, JSON) from the connected devices"
    doLast {
        getADB().devices.each { device ->
            def cucumberPath = getDeviceCucumberPath(device)
            def files = getCucumberReportFileNames()
            files.each { fileName ->
                def deviceFileName = "${cucumberPath}/${fileName}"
                def shellCommand = "if [ -d ${cucumberPath} ]; then rm -r ${deviceFileName}; fi"
                def output = executeAdbShellOnDevice(shellCommand, device)
                println output
            }
        }
    }
}

/**
 * Sets the required permissions for Cucumber to write on the internal storage.
 */
task grantPermissions(dependsOn: 'installDebug') {
    doLast {
        // We only set the permissions for the main application
        def adb = adbPath()
        def mainPackageName = android.defaultConfig.applicationId
        def readPermission = "android.permission.READ_EXTERNAL_STORAGE"
        def writePermission = "android.permission.WRITE_EXTERNAL_STORAGE"
        getADB().devices.each { device ->
            exec { commandLine adb, "-s", device.getSerialNumber(), 'shell', 'pm', 'grant', mainPackageName, readPermission }
            exec { commandLine adb, "-s", device.getSerialNumber(), 'shell', 'pm', 'grant', mainPackageName, writePermission }
        }
    }
}

/**
 * Checks if there's at least one connected device.
 */
task checkDeviceConnection {
    doLast {
        def bridge = getADB()
        long timeOut = 30000
        int sleepTime = 1000
        while (!bridge.hasInitialDeviceList() && timeOut > 0) {
            sleep sleepTime
            timeOut -= sleepTime
        }
        if (timeOut <= 0 && !bridge.hasInitialDeviceList()) {
            throw new RuntimeException("Timeout getting device list.", null)
        }
        IDevice[] devices = bridge.devices
        if (devices.length == 0) {
            throw new RuntimeException("No connected devices!", null)
        }
    }
}

//Check if there are devices connected
connectedCheck.dependsOn checkDeviceConnection
connectedCheck.mustRunAfter checkDeviceConnection

// Delete existing Cucumber reports on the device before connected test-run
connectedCheck.dependsOn deleteExistingCucumberReports
connectedCheck.mustRunAfter deleteExistingCucumberReports

// Grant storage permissions to store Cucumber reports before connected test-run
connectedCheck.dependsOn grantPermissions
connectedCheck.mustRunAfter grantPermissions

// Store the new cucumber reports to the reports directory after connected test-run
connectedCheck.finalizedBy downloadCucumberReports

// ==================================================================
// Utility methods
// ==================================================================

/**
 * Utility method to get the full ADB path
 * @return the absolute ADB path
 */
String adbPath() {
    def adb = android.getAdbExecutable().getPath()
    if (adb.isEmpty()) {
        throw new GradleException("Could not detect adb path")
    }
    return adb
}

/**
 * Method to get the ADB object.
 * @return AndroidDebugBridge
 */
def getADB() {
    AndroidDebugBridge.initIfNeeded(false /*clientSupport*/)
    def bridge = AndroidDebugBridge.createBridge(adbPath(), false /*forceNewBridge*/)
    if (!bridge.isConnected()) {
        bridge.restart()
    }
    return bridge
}

/**
 * Sometime adb returns '\r' character multiple times.
 * @param s the original string returned by adb
 * @return the fixed string without '\r'
 */
static def fixAdbOutput(String s) {
    return s.replaceAll("[\r\n]+", "\n").trim()
}
/**
 * Runs the adb tool
 * @param program the program which is executed on the connected device
 * @return the output of the adb tool
 */

static def executeAdbShellOnDevice(String program, IDevice device) {
    def receiver = new CollectingOutputReceiver()
    device.executeShellCommand(program, receiver)
    return fixAdbOutput(receiver.getOutput())
}

/**
 * Gets the 'external' storage path which is confusingly the internal storage of the phone.
 * @return
 */

static def getDeviceExternalStoragePath(IDevice device) {
    String r = executeAdbShellOnDevice('echo $EXTERNAL_STORAGE', device).trim()
    if (!r.matches('(/[^/]+)+')) {
        throw new GradleException("Could not detect external storage path")
    }
    return r
}

/**
 * The path which is used to store the Cucumber files.
 * @return
 */
static def getDeviceCucumberPath(IDevice device) {
    return getDeviceExternalStoragePath(device) + '/cucumber'
}

/**
 * @return the known Cucumber report files/directories
 */
static def getCucumberReportFileNames() {
    return ['cucumber.json', 'cucumber.xml', 'cucumber.html']
}
